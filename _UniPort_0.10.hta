
<HTML>
  <HEAD>
  <TITLE>UniPort</TITLE>
 
      <HTA:APPLICATION
      Application ID = "vvcbHTA"
      APPLICATIONNAME = "vvcb"
      BORDER = "thin"
      BORDERSTYLE = "complex"
      CAPTION = "Yes"
      CONTEXTMENU = "yes"
      ICON = ""
      INNERBORDER = "no"
      MAXIMIZEBUTTON = "no"
      MINIMIZEBUTTON = "yes"
      NAVIGABLE = "no"
      SCROLL = "Yes"
      SCROLLFLAT = "Yes"
      SELECTION = "No"
      SHOWINTASKBAR = "Yes"
      SINGLEINSTANCE = "Yes"
      SYSMENU = "yes"
      VERSION = "0.10"
      WINDOWSTATE = "Normal"
      />
  </HEAD>
<style>
* html {height: 100%; overflow:hidden;}

  body {
 
  font-family:sans-serif;
  font-size:x-small;
  height:100%
  overflow: auto;
  }
 
  h3 {
  padding:1px;
  margin:1px;
  font-size:small;
  }
  
  .message {
  padding:1px;
  margin:1px;
  font:bold;
  font-size:xx-small;
  }
  
  .statusgood {
  padding-left:10px;
  margin:1px;
  font:bold;
  font-size:xx-small;
  color:#009900;
  }
  
  .statusbad {
  padding-left:10px;
  margin:1px;
  font:bold;
  font-size:xx-small;
  color:#E80000  
  }
  
  form {
  padding:1px;
  margin:1px;
  }
  
  #MDTList {
  position:fixed;
  font-size:xx-small;
  padding:1px;
  margin:1px;
  overflow: auto;
  } 
  
  select {
   background: transparent;
   width: 45px;
   padding: 1px;
   font-size: xx-small;
   line-height: 1;
   border: 1px solid #ccc;
   border-radius: 0;
   height: 20px;
   -webkit-appearance: none;
   }
   
	.table {
	width:auto;	
	border: 0px;
	font-size: xx-small;
   }
    
</style>

<SCRIPT LANGUAGE="VBScript">
Dim ChangeLog, boolChangeLog
boolChangeLog = False
ChangeLog = "<b>ChangeLog 010914</b><br>" &_
	"- version 0.09 -> 0.10<br>" &_
	"- Inactivate user input while loading a patient.<br>" &_
	"- Window_onUnLoad - remove temporary files - sleeper.vbs and temp_letter.html<br>" &_
	"- Repositioned specialty chooser<br>" &_
	"- Specialty chooser available only when T_Drive is checked<br>" &_
	"- F£$% IE - CSS tables dont work. Reverting to tables<br>" &_
	"- Added time-out to Evolve - when Evolve opens a popup " &_
	"- Reformatted variables and constants<br>" &_
"<b>ChangeLog 120814</b><br>" &_
	"- version 0.08->0.09<br>" &_
	"- Ability to choose specialty<br>" &_
"<b>ChangeLog 010814</b><br>" &_
	"- version 0.07->0.08<br>" &_
	"- Pass arguments ByVal rather than ByRef<br>" &_
"<b>ChangeLog 290714</b><br>" &_
	"- version 0.06->0.07<br>" &_
	"- Some error trapping in OpenTLetter that caused missing object error with MS Word<br>" &_
	"- Move all id validation into one function<br>" &_
	"- IE becomes visible after loading is complete<br>" &_
"<b>ChangeLog 250714</b><br>" &_
	"- MDT List labels change color when they have been clicked<br>" &_
	"- IE opening with only T-drive checked with no letter resolved<br>" &_
	"- New 'Clear MDT List' button<br>" &_
	"- Error messages tweaked<br>" &_
	"- Window location moved slightly to accomodate QuickLaunch Bar<br>" &_
"<b>ChangeLog 240714</b><br>" &_
	"- MDT List<br>" &_
"<b>ChangeLog 220714</b><br>" &_
	"- Validate user input<br>" &_
	"- Validate NHS number<br>" &_
	"- Ability to search by MRN<br>" &_
	"- Get NHS number, hospital number and Patient Name from Evolve<br>" &_
	"- Use MRN number from Evolve to search ALMA when NHS number is used<br>" &_
	"- Use NHS number from Evolve to search T-Drive when MRN is used<br>" &_
	"- Fixed ALMA search issue - searches MRN when MRN entered, else does freetext search<br>" &_
"<b>Disclaimer: </b><br>" &_
	"A lot of the stuff in this application has been learnt over the past " &_
	"few weeks on online tutorials and fora. I have simply used " &_
	"building blocks from various places to create this application. While " &_
	"the idea behind the program and how it operates is mine, " &_
	"I claim no expertise in VBScript or HTML applications. " &_
	"And, it goes without saying that I take no responsibility..." &_
	"...if your cat dies as a consequence!<br>"  &_
	"And, don't ask why HTA, unless you have an alternative!!"
 Dim Help, boolHelp
 boolHelp = False
 Help = "<b> ALWAYS CHECK THAT THE CORRECT DOCUMENTS HAVE BEEN OPENED.</b><br><br>" &_ 
 "<b>In case of <a href='http://en.wikipedia.org/wiki/Cybernetic_revolt'>Cybernetic revolt</a>:</b><br>" &_
"I am sure there are plenty of bugs in this and the whole thing may set fire to your cat if you are not careful.<p>If it crashes and burns, try to close it down. </p>" &_
"If that doesn't work,<br>" &_
 "- press ctrl+alt+delete<br>" &_
 "- click on 'Start Task Manager'<br>" &_
 "- goto 'Processes' tab<br>" &_
 "- select 'mshta.exe' in the first column <br>" &_
 "- press delete key. <br>" &_
 "- Click OK and you should be good.<br><br>" &_
 "<b>MDT List</b><br>" &_
"- This depends on the MDT coordinator sending out the list roughly in the same format." &_
"Save this file in a suitable location.<br>" &_
"- Click on 'MDT' and open the MDT list MS Word document<br>" &_
"- The program automatically reads the file for names and NHS numbers<br>" &_
"- It then makes itself taller and lists these below the existing stuff<br>" &_
"- Clicking on any patient will open the patient in whatever portal has been chosen at the top<br>"


 
 'T:\SurgicalServices-RPH\Patient Letters Surgery, Head & Neck\Urology
 'T:\SurgicalServices-RPH\Patient Letters Surgery, Head & Neck\General Surgery\Vascular
 'T:\SurgicalServices-RPH\Patient Letters Surgery, Head & Neck\General Surgery\General Surgery
 
'######################File Parameters#####################

'Error/Status Messages
const M_OPENED = "<span class='statusgood'>...Opened</span>. "
const M_NOT_FOUND = "<span class='statusbad'>Not found.</span>"
const M_CHOOSE_PORTAL = "<span class='statusbad'>Choose a portal!</span>"
const M_NO_ID_ENTERED = "<span class='statusbad'>Enter NHS/MRN Id!</span>"
const M_BAD_ID = "<span class='statusbad'>Invalid NHS/MRN number!</span>"
const M_NO_NHS_ID_FROM_EVOLVE = "<span class='statusbad'>No NHSId!</span>"
const M_WINDOW_CLUTTER = "Beware of window clutter!"
const M_CHOOSE_SPECIALTY = "<span class='statusbad'>Choose a specialty!</span>"
const M_TIMEOUT = "<span class='statusbad'>Timed out!</span>"
const WINDOW_WIDTH = 210
const WINDOW_HEIGHT = 230
const WINDOW_X = 5
const WINDOW_Y = 30

'File extensions
Dim fileExtensions
fileExtensions = Array (".doc", ".docx", ".doc.imported", ".docx.imported")

'########################End of Parameters##########################

'I think these are the only Global variables
'Try to keep scope of all other variables local/private
Dim nhsId, mrnId, id, patientName, patientDOB, boolTabs, ie, countEaster, boolWorking
dim cA,cE,cT, spl
boolTabs = false
boolWorking = False
'TDrive locations - Set directly in HTML
'Default windowsize and location - variables - can be changed later
call htaWindow(WINDOW_WIDTH,WINDOW_HEIGHT,WINDOW_X,WINDOW_Y)

Sub htaWindow(htaWidth,htaHeight,htaTop,htaLeft)
   window.resizeTo htaWidth, htaHeight
   window.moveTo htaTop,htaLeft
End Sub

'Sub Window_onLoad
'End Sub

Sub Window_onUnLoad
'Tidy up temporary files
	Dim fso
	Set fso = CreateObject("Scripting.FileSystemObject")
	If fso.FileExists("sleeper.vbs") = True Then fso.DeleteFile("sleeper.vbs")
	If fso.FileExists("temp_letter.html") = True Then fso.DeleteFile("temp_letter.html")
	If fso.FolderExists("temp_letter_files") = True Then fso.DeleteFolder("temp_letter_files")
End Sub

Sub checkSubmit
	'Enter key behaviour
	If boolWorking = True Then Exit Sub
	if window.event.keyCode = 13 Then
		findPatient
		window.event.returnvalue=false
	End If
End Sub

Sub Message(ByVal m)
	ErrorMessage.InnerHTML = m
End Sub

Sub findPatient
	'run this only if current running state is False
	If boolWorking = True Then Exit Sub
	boolWorking = True
	cA = Document.frmInput.chkALMA.checked
	cE = Document.frmInput.chkEvolve.checked
	cT = Document.frmInput.chkTDrive.checked
	id = Replace(Document.frmInput.txtId.Value," ","")
	spl = Document.frmInput.selectSpecialty.value
	
	'If none of the options are chosen, display error message and abort
	if cE = False and cA = False And cT = False Then
		Message(M_CHOOSE_PORTAL)
		ExitActions
		exit sub
	End If
	
	if id ="" then
		Message(M_NO_ID_ENTERED)
		ExitActions
		Exit Sub
	end if
	
	if cT = true and spl="" Then
		Message(M_CHOOSE_SPECIALTY)
		ExitActions
		Exit Sub
	End If

	'Validation gives idType which can be NHS, MRN or Error
	idType = ValidateID(id)
	
	'Exit with error message if id is wrongly formatted
	if idType = "Error" Then
		Message(M_BAD_ID)
		ExitActions
		Exit Sub
	End If
	
	Message("Loading.....")

	'Start Internet Explorer
	Set ie = CreateObject("InternetExplorer.Application")
	'ie.Visible = 1
		
	'if evolve is checked, open it regardless of type of id
	'this should hopefully pick up mrn and nhs ids
	if cE then EvolveStatus.InnerHTML = OpenEvolve(id)
	
	if idType = "NHS" Then
		'Search ALMA using mrnId where possible
		if cA Then
			if mrnId<>"" Then
				ALMAStatus.InnerHTML = OpenALMA(mrnId,"MRN")
			Else
				ALMAStatus.InnerHTML = OpenALMA(id,"NHS")
			End If
		End If
		if cT Then TDriveStatus.InnerHTML = OpenTLetterInIE(id)

	elseif idType = "MRN" Then
		if cA Then ALMAStatus.InnerHTML = OpenALMA(id,"MRN")
		
		if cT Then 
		'if evolve was not chosen, then choose it to retrieve NHSId
			if cE = False Then
				Document.frmInput.chkEvolve.checked  = True
				cE = True
				EvolveStatus.InnerHTML = OpenEvolve(id)
			End If
			'Above line should have retrieved all patient identifiers from Evolve
			'Make sure nhsId contains a value
			if nhsId<>"" Then
				TDriveStatus.InnerHTML = OpenTLetterInIE(nhsId)
			Else
				TDriveStatus.InnerHTML = M_NO_NHS_ID_FROM_EVOLVE
			End If
		End If
	End If
	'Check if ie has not already been set to nothing by TDrive
	if ie <> "" then 
		ie.Visible = 1
		set ie = Nothing
	end if
	Message("Loading complete!")

	'Reset Everything
	ExitActions
	
End Sub

Sub ExitActions()
	'Call this before every exit sub (not end sub)
	Document.frmInput.txtId.Value=""
	boolTabs = false
	nhsId = ""
	mrnId = ""
	id = ""
	patientName = "" 
	patientDOB = ""
	boolWorking = False
End Sub

Function OpenEvolve(ByVal idE)
	Dim urlSearch, urlRecord
	urlSearch = "https://dont-put-this-on-github.aspx"
	urlRecord = "https://dont-put-this-on-github.aspx"

	const ID_INPUT_BOX = "ctl00_TabContent_GenericPanel_1_PanelId_1_PatientSearchInputBox1__TextBox"
	const ID_HOSPNUMBER_INPUT_BOX = "ctl00_TabContent_GenericPanel_1_PanelId_1_txtHospitalNo"
	const ID_NHS_INPUT_BOX = "ctl00_TabContent_GenericPanel_1_PanelId_1_txtNHSNo"
	const ID_CLEAR = "ctl00_TabContent_GenericPanel_1_PanelId_1_Clear"
	const ID_SUBMIT = "ctl00_TabContent_GenericPanel_1_PanelId_1_Submit"
	const ID_HOSP_NUMBER = "ctl00_HospNumberVal"
	const ID_PATIENT_BANNER = "ctl00_pnlPatientBanner"

	ie.Navigate2 urlSearch
	Dim i 'length of wait
	i = 0
	Do While ie.ReadyState<>4
		Sleep(500)
		i = i + 1
		if i>20 then 
			OpenEvolve = M_TIMEOUT
			Exit Function
		End If
	Loop
	'Clear all values in evolve search form first
	'if this does not work, clear every textbox manually
	'this statement avoids loading most recent historical patient
	'did not work - ie.document.getElementById(ID_CLEAR).Click
	ie.document.getElementById(ID_HOSPNUMBER_INPUT_BOX).value = ""
	ie.document.getElementById(ID_NHS_INPUT_BOX).value = ""
	'input new id and submit
	ie.document.getElementById(ID_INPUT_BOX).value = idE
	ie.document.getElementById(ID_SUBMIT).Click
	'First wait till the patient page starts loading
	Do While ie.LocationURL <> urlRecord
		Sleep(500)
		i = i + 1
		if i>20 then 
			OpenEvolve = M_TIMEOUT
			Exit Function
		End If
	Loop 
	'When patient page has started loading, wait till it completes loading
	if ie.LocationURL = urlRecord Then
		Do While (ie.ReadyState<>4) 
			Sleep(500)
			i = i + 1
			if i>20 then 
				OpenEvolve = M_TIMEOUT
				Exit Function
			End If
		Loop
		mrnId = ie.document.getElementById(ID_HOSP_NUMBER).value
		RegExEvolve(ie.document.getElementById(ID_PATIENT_BANNER).InnerHTML)
	End If
	boolTabs = true
	OpenEvolve = M_OPENED
End Function

Function OpenALMA(ByVal idA, myType)
	'Note that there is still a custom url string "&searchstring=" within the OpenALMA function
	Dim almaURL, almaURLstub
	almaURL = "dont-put-this-on-github.aspx?MRN="
	almaURLstub = "&checked=N&checkedmultipleletters=N&dept=All&deptID=All"
	
	'note second argument myType
	'ALMA URL construct varies depending on whether MRN or NHS number is used
	'Hence, the almaURL and almaURLstub variables as well
	
	if myType = "NHS" Then
		'Reformat nhsID as ### ### ####
		idA = Replace(idA," ","")
		idA = left(idA,3) & "+" & mid(idA,4,3) & "+" & right(idA,4)
		almaURL = almaURL & almaURLstub & "&searchstring=" & idA
	elseif myType ="MRN" Then
		almaURL = almaURL & idA & almaURLstub
	end if
	
	if boolTabs = true Then 
		ie.Navigate2 almaURL, 2048
		if err.number<> 0 then 	err.clear
	Else
		ie.Navigate2 almaURL
		boolTabs = true
		if err.number<> 0 then 	err.clear
	End If	
	OpenALMA = M_OPENED
End Function

Function OpenTLetterInIE(ByVal idT)
	Dim fileName, fso, fname
	
	'Reformat nhsID as ### ### ####
	idT = Replace(idT," ","")
	idT = left(idT,3) & " " & mid(idT,4,3) & " " & right(idT,4)
	'Create filename with path
	fileName =  Document.frmInput.selectSpecialty.value & idT
	set fso = CreateObject("Scripting.FileSystemObject")
	'Check if file exists
	for each fileExt in fileExtensions
		if fso.FileExists(fileName&fileExt) Then
			fname = fileName & fileExt
			Exit For
		End If
	next
	'if no file found and Evolve and ALMA were not chosen, then quit IE
	'this unusual scenario causes IE to open and quit without doing anything
	'must find a better way of implementing this.
	if fname = "" Then
		if cE = False and cA = False Then
			ie.Quit
			'set ie = Nothing
		End If
		OpenTLetterInIE = M_NOT_FOUND
		Exit Function
	End If
	
	'this section is to get the current directoy
	'create this everytime so that the old file gets overwritten
	Set objOutputFile = fso.CreateTextFile("temp_letter.html", True)
	Set objOutputFile = fso.GetFile("temp_letter.html")
	objParentDir = objOutputFile.ParentFolder 
	Set objFolder = fso.GetFolder(objParentDir) 
	'### To avoid problems with folder names that contain spaces, e.g. 
	'### "Documents and Settings" or "Program Files", return the 8.3 name. 
	objCurrDir = objFolder.ShortPath 
	
	tempLetterURL = objCurrDir & "\temp_letter.html"
	'tempLetterURL = "temp_letter.html"
	Set oWord = CreateObject("Word.Application")
	oWord.Visible = False
	if oWord <>"" then
		oWord.Documents.Open fname
		set oDoc = oWord.ActiveDocument
	end if
	if oDoc<>"" then
		if patientName<>"" Then
			oDoc.BuiltInDocumentProperties.item("Title").value = "T Drive: " & patientName 
		Else
			oDoc.BuiltInDocumentProperties.item("Title").value = "T Drive: " & idT 
		End If
		oDoc.SaveAs tempLetterURL, 10 '10 is the constant for FilteredHTML

		if boolTabs = true Then
			ie.Navigate2 tempLetterURL, 2048
		Else
			ie.Navigate2 tempLetterURL
		End If
		OpenTLetterInIE = M_OPENED	
		oDoc.close
	else
		'Error no document found
	end if
	oWord.Quit
	set fso = Nothing
	if err.number<> 0 then 	err.clear
End Function

Sub RegExEvolve(ByVal strEvolveText)
	Set oRegExp = New RegExp
	oRegExp.IgnoreCase = True
	oRegExp.Global = True
	
	'NHS number
	oRegExp.Pattern = "[0-9]{3} [0-9]{3} [0-9]{4}"
	if oRegExp.Execute(strEvolveText).Count<>0 then 
		nhsId = oRegExp.Execute(strEvolveText)(0)
	end if
	
	'Date of Birth
	oRegExp.Pattern = "[0-9]{2}/[0-9]{2}/[0-9]{4}"
	if oRegExp.Execute(strEvolveText).Count<>0 then 
		patientDOB = oRegExp.Execute(strEvolveText)(0)	
	end if
	
	'Patient Name
	oRegExp.Pattern = "<b>[a-z, ]+</b>"
	if oRegExp.Execute(strEvolveText).Count<>0 then 
		patientName = oRegExp.Execute(strEvolveText)(0)	
		patientName = Replace(patientName,"<B>","")
		patientName = Replace(patientName,"</B>","")
	end if
	
	if err.number<> 0 then 	err.clear
End Sub

Function ValidateID(ByVal patient_id)
	'this worked on 210714 at 1900hrs - dont change without good reason
	'modified on 300714 to perform all validation here
	If IsNumeric(patient_id) = False  Then
		'ID is not numeric
		ValidateID = "Error"	
	Else
		'validate for nhsid if length is 10 digits
		If len(patient_id) = 10 then 
			'then perform NHS number validation
			dim computedNHS	'http://www.datadictionary.nhs.uk/version2/data_dictionary/data_field_notes/n/nhs_number_de.asp?shownav=0
			'steps 1 to 2 from above site
			Dim i
			i=1
			do while i<10
				computedNHS = computedNHS + CInt(mid(patient_id,i,1))*(11-i)
				i = i+1
			Loop
			'steps 3,4 from above
			checkDigit = 11 - (computedNHS Mod 11)
			'step 5 - if checkdigit = 10 then nhs number is invalid
			if checkDigit = 10 Then
				ValidateID =  "Error"
				Exit Function
			'if checkdigit is 11 then set it to 0 as checkDigit has to be a single digit
			elseif checkDigit = 11 Then
				checkDigit = 0
			End If
			'compare checkDigit with 10th digit in patient_id
			if cStr(checkDigit) = Right(patient_id,1) Then
				ValidateID = "NHS"
			Else
				ValidateID =  "Error"
			end if		
		elseif len(patient_id)<9 Then
			'this is likely to be an MRN
			ValidateID = "MRN"
		Else
			'I don't think any MRN is 9 digits long
			ValidateID = "Error"
		End If
	End If
End Function

Sub Sleep(MSecs) 
	'copied this from a forum
	'provides a sleep function
	Set fso = CreateObject("Scripting.FileSystemObject")
	If fso.FileExists("sleeper.vbs")=False Then
		Set objOutputFile = fso.CreateTextFile("sleeper.vbs", True)
		objOutputFile.Write "wscript.sleep WScript.Arguments(0)"
		objOutputFile.Close
	End If
	CreateObject("WScript.Shell").Run "sleeper.vbs " & MSecs,1 , True
End Sub

Sub clearMessages
	TDriveStatus.InnerHTML = ""
	EvolveStatus.InnerHTML = ""
	ALMAStatus.InnerHTML = ""
	Message(M_WINDOW_CLUTTER)
End Sub
 
Function GetMDTList
	Dim strContents
	 'http://technet.microsoft.com/en-us/library/ee692906.aspx
	 Const msoFileDialogOpen = 1
	 Set objWord = CreateObject("Word.Application")
	 objWord.ChangeFileOpenDirectory("H:\")
	 objWord.FileDialog(msoFileDialogOpen).Title = "Open MDT Patient List"
	 objWord.FileDialog(msoFileDialogOpen).AllowMultiSelect = False
	 If objWord.FileDialog(msoFileDialogOpen).Show = -1 Then
		objWord.WindowState = 2
		For Each objFile in objWord.FileDialog(msoFileDialogOpen).SelectedItems
			if objFile<>"H:\" then 
				objWord.Documents.Open objFile
				Exit For 
			End If
			'exit after opening the first file. 
			'can't open multiple files anyway as multi-select is set to false.
		Next
	Else
		ErrorMessage.InnerHTML = "<span class='statusbad'>" & "No MDT List chosen!" & "</span>"
		Exit Function
	End If
	
	objWord.visible = False
	set objDoc = objWord.ActiveDocument
	
	Set oRegExp = New RegExp
	oRegExp.IgnoreCase = True
	oRegExp.Global = True
	
	strContents = objDoc.Range.Text
		
	'Find entire block of patient identifiable infomation
	oRegExp.Pattern = "([a-z ]*)(.){1}([0-9]{3} [0-9]{3} [0-9]{4})"
	if oRegExp.Test(strContents) Then
		MDTList.InnerHTML = "<b>>>>Click to Open<<<</b>"
		set oMatchCollection = oRegExp.Execute(strContents)
		for each oMatch in oMatchCollection
			oRegExp.Pattern = "([0-9]{3} [0-9]{3} [0-9]{4})"
			oneNHSId = Replace(oRegExp.Execute(oMatch)(0)," ","")
			oRegExp.Pattern = "[a-z ]*"
			onePatientName = oRegExp.Execute(oMatch)(0)
			ptHTML = "<span id=" & oneNHSId & " onclick='findById(" & oneNHSId & ")'><b>" & onePatientName & "</b><br>"
			ptHTML = ptHTML & oneNHSId & "</span>"
			MDTList.InnerHTML = MDTList.InnerHTML & "<hr>" & ptHTML
		next
	Else
		ErrorMessage.InnerHTML = "<span class='statusbad'>" & "Unable to parse file!" & "</span>"
		Exit Function
	End If
		'MDTList.InnerHTML = strContents
	objDoc.Close
	objWord.Quit
	set oRegExp = Nothing
	set objDoc = Nothing
	set objWord = Nothing
	call htaWindow(WINDOW_WIDTH,800,WINDOW_X,WINDOW_Y)
End Function

Sub findById(list_id)
	If boolWorking = True Then Exit Sub
	Document.frmInput.txtId.Value = list_id
	Document.getElementById(list_id).style.Color = "#800000"
	findPatient
End Sub

Sub ClearMDTList()
	call htaWindow(WINDOW_WIDTH,WINDOW_HEIGHT,WINDOW_X,WINDOW_Y)
	MDTList.InnerHTML = ""
	clearMessages
End Sub

Sub chkTDriveChanged()
	if Document.frmInput.chkTDrive.checked Then
		Document.frmInput.selectSpecialty.style.visibility =""
	else 
		Document.frmInput.selectSpecialty.style.visibility = "hidden"
	End If
End Sub

Sub DisplayChangeLog()
	if boolChangeLog = False Then
		MDTList.InnerHTML = ChangeLog
		call htaWindow(WINDOW_WIDTH,800,WINDOW_X,WINDOW_Y)
		boolChangeLog = True
	Else
		MDTList.InnerHTML =""
		call htaWindow(WINDOW_WIDTH,WINDOW_HEIGHT,WINDOW_X,WINDOW_Y)
		boolChangeLog = False
	End If
End Sub

Sub DisplayHelp()
	if boolHelp = False Then
		MDTList.InnerHTML = Help
		call htaWindow(WINDOW_WIDTH,800,WINDOW_X,WINDOW_Y)
		boolHelp = True
	Else
		MDTList.InnerHTML =""
		call htaWindow(WINDOW_WIDTH,WINDOW_HEIGHT,WINDOW_X,WINDOW_Y)
		boolHelp = False
	End If
End Sub

 </SCRIPT>

<body>
<h3>Enter NHS/MRN Id</h3>

	<form name="frmInput">
		<input name="txtId" id ="txtId" type="text" size="12" value="" onkeydown="checkSubmit" onfocus="clearMessages">
		<INPUT type="button" value="Find"  onclick="findPatient" name ="btnFind" class="button"><br>
		<span id="ErrorMessage" class="message">Beware of window clutter!</span><br>
		<table class='table'>
			<tr><td><input name="chkTDrive" type="checkbox" checked onclick="chkTDriveChanged">T:
			<select name="selectSpecialty" id="selectSpecialty">
			<option value="">??</option>
			<option value="T:\SurgicalServices-RPH\Patient Letters Surgery, Head & Neck\General Surgery\Colorectal\">CR</option>
			<option value="T:\SurgicalServices-RPH\Patient Letters Surgery, Head & Neck\Upper GI\">OG</option>
			</select></td><td><span id="TDriveStatus"></span></td></tr>
			<tr><td><input name="chkEvolve" type="checkbox" checked title="Leave this checked to extract patient details.">Evolve</td>	<td><span id="EvolveStatus"> </span></td></tr>
			<tr><td><input name="chkALMA" type="checkbox" checked>ALMA</td><td><span id="ALMAStatus"></span></td></tr>			
		</table>
	</Form>

<div>
	<input type="button" Value="MDT"  onclick="GetMDTList" class="button">
	<input type="button" Value="Clear MDT"  onclick="ClearMDTList" class="button">
</div>
<span class="message"><a href="http://uk.linkedin.com/pub/vishnu-v-chandrabalan/41/2b7/a77">vvcb</a> <span onclick="DisplayChangeLog">v0.10a</span> 010914 <span onclick="DisplayHelp">   Help</span></span>
<div id="MDTList"></div>
</body>
</html>
